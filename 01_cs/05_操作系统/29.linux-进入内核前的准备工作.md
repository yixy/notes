# linux-进入内核前的准备工作

## 0 进入内核前的准备工作

![](https://raw.githubusercontent.com/yixy4app/images/picgo/202406301450557.JPG)

重新设置内存到零地址。

![](https://raw.githubusercontent.com/yixy4app/images/picgo/202406301547998.JPG)

最终内存布局为：

![](https://raw.githubusercontent.com/yixy4app/images/picgo/202406301547744.JPG)

## 1 bootsect.s：从硬盘装载最开始的代码

从0xFFFF0处执行ROM指令，开始BIOS自检，并将磁盘启动扇区512B内容拷贝到内存0x7c00处。

```shell
;1.初始化PC寄存器，0xFFFF0对应ROM中BIOS地址
;  pc=0xFFFF0
;2.开始执行BIOS自检，并检测磁盘第一扇区是否为启动扇区（前512B的最后两个字节为0x55,0xaa）
;  若为启动扇区则BIOS将该扇区数据复制到内存0x7c00处。
```

开始执行启动扇区代码。将内存地址 0x7c00 处开始往后的 512 字节的数据，原封不动复制到 0x90000 处开始的后面 512 字节的地方。并且跳转到 0x90000 加上 go 这个标签所代表的偏移量，也就是 `mov ax,cs`指令的地址。随后开始初始化数据段和栈相关寄存器。

```shell
;x86为了在16位实模式下能访问到20位的地址线，所以会把段基址先左移4位。
mov ax,0x07c0
mov ds,ax

;ds=0x9000
mov ax,0x9000
;es=0x9000
mov es,ax
;cs=256
mov cx,#256
;si=0
sub si,si
;di=0
sub di,di
;重复执行movw
;movw每次从ds:si复制16位到es:di，执行cx次（256次），注意在每次执行后 SI 和 DI 会自动地址向下一个要搬移的位址
rep movw

;跳转至0x9000+go的地址，即mov ax,cs对应的地址。
;cs=0x9000
;ip=go
jmpi go，0x9000
go:
    mov ax,cs
    mov ds,ax
    mov es,ax
    mov ss,ax
    ;指向栈顶，ss:sp=0x9FF00
    mov sp,#0xFF00
```

从硬盘的第 2 个扇区开始，把数据加载到内存 0x90200 处，共加载 4 个扇区。如果执行成功，再把从硬盘第 6 个扇区开始往后的 240 个扇区，加载到内存 0x10000 处。至此，整个操作系统的全部代码，就已经全部从硬盘加载到内存中了。最后代码通过段间跳转指令 jmpi 0,0x9020，跳转到 0x90200 处，就是硬盘第二个扇区开始处的内容。

```shell
load_setup:
    mov dx,#0x0000      ; drive 0, head 0
    mov cx,#0x0002      ; sector 2, track 0
    mov bx,#0x0200      ; address = 512, in 0x9000
    mov ax,#0x0200+4    ; service 2, nr of sectors
    int 0x13            ; read it. BIOS中预置的中断处理例程，读磁盘。
    jnc ok_load_setup       ; ok - continue
    mov dx,#0x0000
    mov ax,#0x0000      ; reset the diskette
    int 0x13
    jmp load_setup

ok_load_setup:
    ...
    mov ax,#0x1000
    mov es,ax       ; segment of 0x10000
    call read_it
    ...
    jmpi 0,0x9020
```

## 2 setup.s：加载系统设备信息+重新设置内存布局、完成实模式向保护模式转换

开始执行setup.s的代码。

文字模式，在这种模式下，一屏幕可以显示 25 行，每行 80 个字符，也就是 80 列。int 0x10 中断程序执行完毕并返回时，将会在 dx 寄存器里存储好光标的位置，具体说来其高八位 dh 存储了行号，低八位 dl 存储了列号。

```shell
start:
    mov ax,#0x9000  ; this is done in bootsect already, but...
    mov ds,ax
    mov ah,#0x03    ; read cursor pos
    xor bh,bh
    int 0x10        ; save it in known place, con_init fetches
    mov [0],dx      ; it from 0x90000.
```

通过调用BIOS中断获取系统信息，并存储覆盖到内存中。

```shell
比如获取内存信息。
; Get memory size (extended mem, kB)
    mov ah,#0x88
    int 0x15
    mov [2],ax
获取显卡显示模式。
; Get video-card data:
    mov ah,#0x0f
    int 0x10
    mov [4],bx      ; bh = display page
    mov [6],ax      ; al = video mode, ah = window width
检查显示方式并取参数
; check for EGA/VGA and some config parameters
    mov ah,#0x12
    mov bl,#0x10
    int 0x10
    mov [8],ax
    mov [10],bx
    mov [12],cx
获取第一块硬盘的信息。
; Get hd0 data
    mov ax,#0x0000
    mov ds,ax
    lds si,[4*0x41]
    mov ax,#INITSEG
    mov es,ax
    mov di,#0x0080
    mov cx,#0x10
    rep
    movsb
获取第二块硬盘的信息。
; Get hd1 data
    mov ax,#0x0000
    mov ds,ax
    lds si,[4*0x46]
    mov ax,#INITSEG
    mov es,ax
    mov di,#0x0090
    mov cx,#0x10
    rep
    movsb
```

注意，0x90000 开始往上的位置，原来是 bootsect 和 setup 程序的代码，而此时 bootsect 的代码现在已经被一些临时存放的数据，如内存、硬盘、显卡等信息，覆盖了一部分。

cli，表示关闭中断的意思。因为后面我们要覆盖掉原本 BIOS 写好的中断向量表，也就是破坏掉原有的表，写上我们自己的中断向量表，所以此时是不允许中断进来的。

```shell
cli         ; no interrupts allowed ;

; first we move the system to it's rightful place
    mov ax,#0x0000
    cld         ; 'direction'=0, movs moves forward
do_move:
    mov es,ax       ; destination segment
    add ax,#0x1000
    cmp ax,#0x9000
    jz  end_move
    mov ds,ax       ; source segment
    sub di,di
    sub si,si
    mov cx,#0x8000
    rep movsw
    jmp do_move
; then we load the segment descriptors
end_move:
    ...
```

此时，system 模块的代码被移动到了内存的零地址处，同时在 0x90000 的位置处存放了一些临时信息，如内存、硬盘、显卡等设备信息，供后面的程序使用。由此也可以看出，system 才是真正被视为重要的操作系统代码，其他的都是作为前期的铺垫，用完就被无情抛弃了。

### 实模式与保护模式

Intel CPU因为x86 的历史包袱，需要进行模式转换，才能从启动时的 16 位的实模式转变为 32 位保护模式。

* 实模式寻址方式：段基址左移四位，再加上偏移地址。
* 保护模式寻址方式：实模式下段寄存器（比如 ds、ss、cs）里存储的是段选择子，根据段选择子中的段描述符索引去全局描述符表中寻找段描述符，段描述符里存储着段基址。从中取出段基址，然后再加上偏移地址，就得到了最终的线性地址（虚拟地址？）。

> 由操作系统把setup块中一个相对位置信息存储在一个叫 gdtr 的寄存器中，告诉 CPU 全局描述符表（gdt）在内存中的什么位置。

> idtr寄存器中存储了异常表（中断描述符表）的地址，其原理和全局描述符表一样。发生中断时，CPU 会拿着中断号从中断描述符表里寻找中断处理程序的地址，找到以后，就会跳转到相应的中断程序去执行。


```shell
; lgdt 就表示把后面的值（gdt_48标签对应地址）放在 gdtr 寄存器中
lidt  idt_48      ; load idt with 0,0
lgdt  gdt_48      ; load gdt with whatever appropriate

idt_48:
    .word   0     ; idt limit=0
    .word   0,0   ; idt base=0L

gdt_48:
    .word   0x800       ; gdt limit=2048, 256 GDT entries
    .word   512+gdt,0x9 ; gdt base = 0X90200+gdt, gdt是setup文件中gdt标签相对位置

;全局描述符表有三个段描述符，第一个为空，第二个是代码段描述符（type=code），第三个是数据段描述符（type=data）。
;第二个和第三个段描述符的段基址都是 0，也就是之后在逻辑地址转换物理地址的时候，通过段选择子查找到无论是代码段还是数据段，取出的段基址都是 0，那么物理地址将直接等于程序员给出的逻辑地址（准确说是逻辑地址中的偏移地址。
gdt:
    .word   0,0,0,0     ; dummy

    .word   0x07FF      ; 8Mb - limit=2047 (2048*4096=8Mb)
    .word   0x0000      ; base address=0
    .word   0x9A00      ; code read/exec
    .word   0x00C0      ; granularity=4096, 386

    .word   0x07FF      ; 8Mb - limit=2047 (2048*4096=8Mb)
    .word   0x0000      ; base address=0
    .word   0x9200      ; data read/write
    .word   0x00C0      ; granularity=4096, 386
```

突破地址信号线 20 位的宽度，变成 32 位可用。这是由于 早期8086 CPU 只有 20 位的地址线，所以如果程序给出 21 位的内存地址数据，那多出的一位就被忽略了。

```shell
;打开 A20 地址线
mov al,#0xD1        ; command write
out #0x64,al
mov al,#0xDF        ; A20 on
out #0x60,al
```

可编程中断控制器 8259 芯片进行的编程。重新编程之后，8259 这个芯片的引脚与中断号的对应关系，变成了后面这样

```shell
; well, that went ok, I hope. Now we have to reprogram the interrupts :-(
; we put them right after the intel-reserved hardware interrupts, at
; int 0x20-0x2F. There they won't mess up anything. Sadly IBM really
; messed this up with the original PC, and they haven't been able to
; rectify it afterwards. Thus the bios puts interrupts at 0x08-0x0f,
; which is used for the internal hardware interrupts as well. We just
; have to reprogram the 8259's, and it isn't fun.

    mov al,#0x11        ; initialization sequence
    out #0x20,al        ; send it to 8259A-1
    .word   0x00eb,0x00eb       ; jmp $+2, jmp $+2
    out #0xA0,al        ; and to 8259A-2
    .word   0x00eb,0x00eb
    mov al,#0x20        ; start of hardware int's (0x20)
    out #0x21,al
    .word   0x00eb,0x00eb
    mov al,#0x28        ; start of hardware int's 2 (0x28)
    out #0xA1,al
    .word   0x00eb,0x00eb
    mov al,#0x04        ; 8259-1 is master
    out #0x21,al
    .word   0x00eb,0x00eb
    mov al,#0x02        ; 8259-2 is slave
    out #0xA1,al
    .word   0x00eb,0x00eb
    mov al,#0x01        ; 8086 mode for both
    out #0x21,al
    .word   0x00eb,0x00eb
    out #0xA1,al
    .word   0x00eb,0x00eb
    mov al,#0xFF        ; mask off all interrupts for now
    out #0x21,al
    .word   0x00eb,0x00eb
    out #0xA1,al
```

好了，接下来的一步，就是真正切换模式的一步了。前两行代码，将 PSW寄存器的PE位 0 置 1，模式就从实模式切换到保护模式了。段间跳转指令 jmpi，后面的 8 表示 cs 寄存器的值，0 表示 ip 寄存器的值。对照上面段选择子的结构，可以知道描述符索引值是 1，也就是 CPU 要去全局描述符表（gdt）中找索引 1 的描述符。零地址处是什么呢？回顾之前的内存布局图，就是操作系统全部代码的 system 这个大模块。

```shell
mov ax,#0x0001  ; protected mode (PE) bit
lmsw ax      ; This is it;
jmpi 0,8     ; jmp offset 0 of segment 8 (cs), set CS=8,IP=0，按段选择子结构，8对应代码段
```

## 3 head.s：

目前为止，0 位置处存储着操作系统全部核心代码，是由 head.s 和 main.c 以及后面的无数源代码文件编译并链接在一起而成的 system 模块。

head.s的代码如下：

```shell
;表示页目录，之后在设置分页机制时，页目录会存放在这里，也会覆盖这里的代码。
_pg_dir:
_startup_32:
    mov eax,0x10
    mov ds,ax   ;第二个段描述符，即数据段描述符
    mov es,ax
    mov fs,ax
    mov gs,ax
    lss esp,_stack_start    ;ss:esp 这个栈顶指针，指向了 _stack_start 这个标号的位置。
```

这个 stack_start 标号定义在了很久之后才会讲到的 sched.c 里，我们这里拿出来分析一波。首先，stack_start 结构中的高 16 位是 0x10，将会赋值给 ss 栈段寄存器，低 32 位是 user_stack 这个数组的末端地址，将其赋值给 esp 寄存器。
赋值给 ss 的 0x10 仍然按照保护模式下的段选择子去解读，其指向的是全局描述符表中的第二个段描述符（数据段描述符），段基址是 0。
赋值给 esp 寄存器的就是 user_stack 数组的末端地址，那最终的栈顶地址，也指向了这里，后面的压栈操作，就是往这个新的栈顶地址处压咯。

```shell
long user_stack[4096 >> 2];

struct {
  long *a;
  short b;
}
stack_start = {&user_stack[4096 >> 2], 0x10};
```

回到head.s。中断描述符表 idt 里面存储着一个个中断描述符，每一个中断号就对应着一个中断描述符，而中断描述符里面主要存储着中断程序的地址。这样一个中断号过来后，CPU 就会自动寻找相应的中断程序，然后去执行它。
那这段程序的作用就是，设置了 256 个中断描述符，并且让每一个中断描述符中的中断程序例程都指向一个 ignore_int 的函数地址，这个是个默认的中断处理程序，之后会逐渐被各个具体的中断程序所覆盖。举个例子，比如之后键盘模块会将自己的键盘中断处理程序，覆盖过去。
现在还没发生这种覆盖行为，所以任何中断对应的中断处理程序，都会指向这个默认的函数 ignore_int，也就是说现在这个阶段你按键盘还不好使。

最后还留了 252 项的空间，这些空间后面会用来放置任务状态段描述符 TSS 和局部描述符表 LDT，这些都是为多任务准备的。

修改了 gdt，所以要重新设置一遍，做个刷新，这样修改才能生效。

为什么原来已经设置过一遍了，这里又要重新设置一遍，你可千万别以为有什么复杂的原因，就是因为原来设置的 gdt 是在 setup 程序中，之后这个地方要被缓冲区覆盖掉，所以这里重新设置在 head 程序中。这样，这块内存区域之后就不会被其他程序用到并且覆盖了。


```shell
call setup_idt ;设置中断描述符表
call setup_gdt ;设置全局描述符表
mov eax,10h
mov ds,ax
mov es,ax
mov fs,ax
mov gs,ax
lss esp,_stack_start

setup_idt:
    lea edx,ignore_int
    mov eax,00080000h
    mov ax,dx
    mov dx,8E00h
    lea edi,_idt
    mov ecx,256
rp_sidt:
    mov [edi],eax
    mov [edi+4],edx
    add edi,8
    dec ecx
    jne rp_sidt
    lidt fword ptr idt_descr
    ret

idt_descr:
    dw 256*8-1
    dd _idt

_idt:
    DQ 256 dup(0)

setup_gdt：
    ...     ;与setup_idt同理，略

_gdt:
    DQ 0000000000000000h    ;/* NULL descriptor */
    DQ 00c09a0000000fffh    ;/* 16Mb */
    DQ 00c0920000000fffh    ;/* 16Mb */
    DQ 0000000000000000h    ;/* TEMPORARY - don't use */
    DQ 252 dup(0)
```

开启分页机制。而这一切的操作，都离不开计算机的一个硬件叫 MMU，中文名叫内存管理单元，有时也叫 PMMU，中文名是分页内存管理单元。这个部件负责的就是把虚拟地址转换为物理地址。
所以整个过程我们不用操心，作为操作系统这个软件层，只需要提供好页目录表和页表，这种页表方案叫做二级页表，第一级叫页目录表 PDE，第二级叫页表 PTE。他们的结构如下：

打开cr0寄存器分页开关

然后，MMU 就可以帮我们进行分页的转换了。此后指令中的内存地址（就是程序员提供的逻辑地址），就统统要先经过分段机制的转换，再通过分页机制的转换，才能最终变成物理地址。


并且跳转到 main 函数。跳转到 main 函数，标志我们正式告别让人头大的汇编，进入 C 语言写的操作系统核心代码。

```shell
jmp after_page_tables
...
after_page_tables:
    push 0
    push 0
    push 0
    push L6
    push _main
    jmp setup_paging    ;设置开启分页机制
L6:
    jmp L6

setup_paging:
    mov ecx,1024*5
    xor eax,eax
    xor edi,edi
    pushf
    cld
    rep stosd
    mov eax,_pg_dir
    mov [eax],pg0+7
    mov [eax+4],pg1+7
    mov [eax+8],pg2+7
    mov [eax+12],pg3+7
    mov edi,pg3+4092
    mov eax,00fff007h
    std
L3: stosd
    sub eax,00001000h
    jge L3
    popf
    xor eax,eax
    mov cr3,eax
    mov eax,cr0
    or  eax,80000000h
    mov cr0,eax
    ret
```

